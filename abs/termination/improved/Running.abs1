module Running;

// Running example of the JAR article

interface Task {}
class TaskC implements Task {}

interface Proc {
  Unit startTask( Task tk );
}

class ProcC implements Proc {
  Unit startTask( Task tk ) {}
}

interface TQ {
	Unit addTasks(List<Task> list);
	Unit consumeAsync(List<Proc> ps);
	Unit consumeSync(List<Proc> ps);
	Unit addTask(Task tk);
}

class TaskQueue implements TQ{
	List<Task> pending=Nil;

	Unit addTasks(List<Task> list){
		while (list != Nil) {
			Task tk = head(list);
			list = tail(list);
			Fut<Unit> f = this ! addTask(tk);
			await f?;
		}
	}

	Unit consumeAsync(List<Proc> ps){
		while (pending != Nil) {
			Task tk = head(pending);
			pending = tail(pending);
			List<Proc> it = ps;
			while( it!=Nil){
				Proc p = head(it);
				p ! startTask(tk);
				it = tail(it);
			}
		}
	}

	Unit consumeSync(List<Proc> ps){
		while (pending != Nil) {
			Task tk = head(pending);
			pending = tail(pending);
			Proc p = head(ps);
			Fut<Unit> f = p ! startTask(tk);
			await f?;
		}
	}
	
	Unit addTask(Task tk){
		pending= appendright(pending,tk);
	}

}

interface M {
	Unit  main1(List<Task> l,List<Proc> ps);
	Unit  main2(List<Task> l,List<Proc> ps);
	Unit  main3(List<Task> l,List<Proc> ps);
	Unit  main4(List<Task> l,List<Proc> ps);
}

class Main implements M {

  // Everything terminating
	Unit  main1(List<Task> l,List<Proc> ps){
		TQ q = new local  TaskQueue();
		q ! addTasks(l);
		q ! consumeAsync(ps);
	}
	
	// Everything terminating
	Unit  main2(List<Task> l,List<Proc> ps){
		TQ q = new local  TaskQueue();
		Fut<Unit> f = q ! addTasks(l);
		await f?;
		q ! consumeSync(ps);
	}
	
	// Everything terminating	
	Unit  main3(List<Task> l,List<Proc> ps){
		TQ q = new local  TaskQueue();
		q ! addTasks(l);
		q ! consumeSync(ps);
	}
	
	// Everything terminating but the 'while (True)' loop (while_2)
	Unit  main4(List<Task> l,List<Proc> ps){
		TQ q = new local  TaskQueue();
		while (True){
			Fut<Unit> x = q ! addTasks(l);
			Fut<Unit> y = q ! consumeSync(ps);
			await x?;
			await y?;
		}
	}
}

