module MailServer;
import * from ABS.StdLib;

type UserName = String ;
type Message = String;


   
def B mylookup<A, B>(Map<A, B> ms, A k) = // retrieve from the map
  case ms {
     InsertAssoc(Pair(k, y), _) => y;
     // InsertAssoc(_, tm) => lookup(tm, k);
     // Rewritten for aPET (to avoid having fresh vars in the nomatch not present in the match)
     InsertAssoc(Pair(_, _), tm) => mylookup(tm, k);
  };

def Map<A, B> myput<A, B>(Map<A, B> ms, A k, B v) =
  case ms {
    EmptyMap => InsertAssoc(Pair(k, v),EmptyMap);
    InsertAssoc(Pair(k, _), ts) => InsertAssoc(Pair(k, v), ts);
    InsertAssoc(p, ts) => InsertAssoc(p, myput(ts, k, v));
  };

interface AddressBook {
      User getUserAddress(UserName u);
      Unit addUser(UserName uname,User u);
}

class AddressBookImp implements AddressBook {
	Map<UserName,User> users = EmptyMap;

	[users <= max(users)]
	User getUserAddress(UserName u){
		return mylookup(users,u);
	}

	[users <= max(users)]
	Unit addUser(UserName uname,User u){
		users = myput(users,uname,u);
	}
}

interface User {
     Unit receive(Message msg);
}

class UserImp implements User {
      List<Message> msgs = Nil;

      Unit receive(Message msg) {
         msgs = Cons(msg,msgs);
      }
}


interface MailServer {
      Unit addUser(UserName uname,User u);
      Unit notify(Message m);
}

class MailServerImp(AddressBook ab) implements MailServer {

	List<UserName> listUsers = Nil;
      
	Unit addUser(UserName uname,User u) {
        listUsers = Cons(uname, listUsers);
		ab!addUser(uname,u);
    }
      
	[listUsers <= max(listUsers)]
	Unit notify(Message m) {
		Fut<User> u; User us;
		List<UserName> listUsersAux = list(listUsers);
		List<UserName> listUsersAux2 = Nil; 
        while ( listUsersAux != Nil ) {
            u = ab ! getUserAddress( head(listUsersAux) );
            [old(listUsersAux) == listUsersAux]
            await u?;
            us = u.get;
            us!receive(m);
            listUsersAux2 = tail(listUsersAux);
            listUsersAux = listUsersAux2;
        }
      }
}

class Main ()  {
	Unit main(Int nUsers, List<Message> mssg) {
		AddressBook ab= new AddressBookImp();
		MailServer ms = new MailServerImp(ab);
		while(nUsers > 0) {		
			User u = new UserImp();
			ms ! addUser("User",u);
			nUsers = nUsers - 1;	  
		}
		List<Message> mssg2 = Nil;
		while (mssg!=Nil){
			ms!notify(head(mssg));
			mssg2=tail(mssg);
			mssg = mssg2;
	    }
	}
}

{ // building three users
    User u1 = new UserImp();
    User u2 = new UserImp();
    User u3 = new UserImp();
    // building the address book and mail server
    AddressBook ab= new AddressBookImp();
    MailServer ms = new local MailServerImp(ab);
    // adding users to the mail server which in turn adds them to the AddressBook
    Fut<Unit> f1 = ms ! addUser("John",u1);
    await f1?;
    Fut<Unit> f2 = ms ! addUser("Mary",u2);
    await f2?;
    Fut<Unit> f3 = ms ! addUser("Mike",u3);
    await f3?;
    List<Message> mssg = Cons("m1",Cons("m2",Cons("m3",Cons("m4",Nil))));
 	List<Message> mssg2 = Nil;
    while (mssg!=Nil){
		ms!notify(head(mssg));
		mssg2=tail(mssg);
		mssg = mssg2;
    }

}