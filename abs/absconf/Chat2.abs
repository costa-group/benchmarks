module ABSChat.Main;

data ServerMsg = Welcome
   	           | GoodBye
          	   | NewMessage(String);

data ClientMsg = ChatMessage(String,String);

interface Server {
   Unit start();
    Session connect(Client c);
}

interface Session {
   Unit receive(ClientMsg m);
   Unit close();
}

interface Client {
	Unit setServer(Server s);
	Unit start();
	Unit receive(ServerMsg m);
	ClientGUI getGUI();
}

interface ClientGUI {
   Unit init2();
   Button getSendButton();
   TextField getTextField();
}

interface ClientIntern extends Client {
	 Unit sendText(String text);
}
 
interface ClientGUIIntern extends ClientGUI {
    ClientIntern getClientIntern();
   String getTextFieldText();
} 

class ClientGUIImpl( ClientIntern client) implements ClientGUIIntern {
   Window gui;
   Button sendButton;   
   TextField textField; 
   Bool initialized = False;
   
   {   
   	  gui = new WindowImpl();
   } 
   
   Unit init2() { 
   	  Fut<Button> f = gui!newButton("Send");
   	  sendButton = f.get;
   	  ButtonListener l = new local SendButtonListener(this);
   	  sendButton!registerListener(l);

      Fut<TextField> tf;
      tf = gui!newTextField();
      textField = tf.get;

   	  initialized = True;
   }
   
   Button getSendButton() {
      await initialized;
      Button b = sendButton;
      return b;
   }
   
   TextField getTextField() {
      await initialized;
      TextField t = textField;
      return t;
   }
   
   String getTextFieldText() {
      Fut<String> f = textField!getText();
      return f.get;
   }
   
   ClientIntern getClientIntern() {
   		ClientIntern c = client;
      	return c;
   }
}

[Plain]
class SendButtonListener([Final]  ClientGUIIntern client) implements ButtonListener {
   Unit pressed() {
       ClientIntern ci = client.getClientIntern();
       String s = client.getTextFieldText();
   	   ci.sendText(s);
   }
}


[COG]
class ClientImpl implements ClientIntern { 
   String name = "Alice";
   Server server;
   Session session;
    ClientGUI gui;

   ClientGUI getGUI() {
	  if (gui == null) {
	     gui = new local ClientGUIImpl(this);
	     gui.init2();
	  }
	  ClientGUI cg = gui;
   	  return cg;
   }

   Unit setServer(Server s) { 
   	   this.server = s;
   }
   
   Unit start() {
      await server != null;
      Fut<Session> f = server!connect(this);
      await f?;
      session = f.get; 
   }
   
   Unit receive(ServerMsg msg) {
   	  await session != null;
	  ClientMsg ans = ChatMessage("","");
	  //if(msg==Welcome){
   	      ans = ChatMessage(name,"Welt"); 
      	  //}
   	  
   	  Fut<Unit> u = session ! receive(ans);
   	  await u?;
   	  session ! close();
   }  
   
   Unit sendText(String text) {
      await session != null;
      session ! receive(ChatMessage(name,text));
   }
}
interface ButtonListener {
   Unit pressed();
}

interface Window {
    Button newButton(String label);
    TextField newTextField();
}

interface TextField {
   String getText();
   Unit setText(String newText);
}

interface Button {
   Unit press();
   String getLabel();
   Unit registerListener(ButtonListener l);
}

class TextFieldImpl implements TextField {
  String text = "";
  String getText() {
     return text;
  }

  Unit setText(String s) {
  	 text = s;
  }
}

[COG]
class WindowImpl implements Window {
    Button newButton(String label) {
      Button b = new local ButtonImpl(this, label);
      return b;
   }
   
   TextField newTextField() {
      TextField t = new local TextFieldImpl();
   	  return t;
   }
}

[Plain] 
class ButtonImpl( Window gui, String label) implements Button {
    List<ButtonListener> listener = Nil;
   
	Unit press() {
		List<ButtonListener> r = listener;
		while (r != Nil) {
			ButtonListener l = head(r);
			l ! pressed();
			r = tail(r);
		}	
	}
	
	String getLabel() {
		return label;
	}
	
	Unit registerListener(ButtonListener l) {
		//listener = Cons(l,listener);
		listener = appendright(listener,l);
	}
}


interface ServerIntern extends Server {
	Unit sessionClosed(SessionIntern i);
}

interface SessionIntern extends Session {
   Unit init2();
}

[COG]
class ServerImpl implements ServerIntern { 
   Bool running = False;
   List<SessionIntern> sessions = Nil;
   
   Unit start() {
      running = True;
   }
   
   Session connect(Client c) {
       await running;
       SessionIntern s;
       s = new local SessionImpl(this, c);
       sessions = Cons(s,sessions);
       s!init2();
       return s;
   }

	Unit sessionClosed(SessionIntern i) {
	   sessions = without(sessions,i);
	}

}

[Plain]
class SessionImpl(ServerIntern server, Client c) implements SessionIntern {
	Unit init2() {
		c!receive(Welcome);
	}
	
	Unit close() {
	   server.sessionClosed(this);
	}
	
	Unit receive(ClientMsg m) {
	}

}

interface User {
   Unit use(ClientGUI client);
}

[COG] 
class UserImpl implements User {
   ClientGUI gui;
   Button sendBtn;
   TextField textField;
	
   Unit use(ClientGUI client) { 
	  gui = client;
	  Fut<Button> f = gui!getSendButton();
	  sendBtn = f.get;
	  
	  Fut<TextField> f2 = gui!getTextField();
	  textField = f2.get;
	  
	  textField!setText("Hello");
	  sendBtn!press();
   }

}

interface Bar {
	Unit m1 ();
	Unit m2 ();
}
class Foo () implements Bar {
	Unit m1() {
	 	Server server = new ServerImpl();  
		server ! start();
		
		Client client = new ClientImpl();
		client ! setServer(server);
		client ! start();
		
		
		Fut<ClientGUI> fgui = client ! getGUI();
		ClientGUI gui = fgui.get;
		
		User user = new UserImpl();
		user ! use(gui);	
	}
	Unit m2() {
        Server server = new ServerImpl();  
		server ! start();
		
		Client client = new ClientImpl();
		client ! setServer(server);
		client ! start();
		
		
		Fut<ClientGUI> fgui = client ! getGUI();
		ClientGUI gui = fgui.get;
		
		User user = new UserImpl();
		user ! use(gui);	
	}
}

{
	Bar o1 = new local Foo();
	Bar o2 = new local Foo();
	
	o1.m1();
	o1.m2();
	//o2.m1();
	//o2.m2();
    
}
